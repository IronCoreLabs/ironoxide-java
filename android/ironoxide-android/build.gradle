plugins {
    id 'signing'
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        // sdk version 24 required to support Java's Optional type
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        ndk {
            abiFilters 'x86', 'arm64-v8a', 'x86_64'
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'Assert'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.ironcorelabs'
            artifactId = 'ironoxide-android'
            version = VERSION_NAME

            pom {
                name = 'ironoxide-android'
                packaging = 'aar'
                description = 'Android library for IronOxide transform encryption'
                url = 'https://github.com/IronCoreLabs/ironoxide-java'
                licenses {
                    license {
                        name = 'AGPL-3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'bobwall23'
                        name = 'Bob Wall'
                        url = 'https://github.com/bobwall23'
                    }
                    developer {
                        id = 'clintfred'
                        name = 'Clint Frederickson'
                        url = 'https://github.com/clintfred'
                    }
                    developer {
                        id = 'coltfred'
                        name = 'Colt Frederickson'
                        url = 'https://github.com/coltfred'
                    }
                    developer {
                        id = 'ernieturner'
                        name = 'Ernie Turner'
                        url = 'https://github.com/ernieturner'
                    }
                    developer {
                        id = 'giarc3'
                        name = 'Craig Colegrove'
                        url = 'https://github.com/giarc3'
                    }
                    developer {
                        id = 'skeet70'
                        name = 'Murph Murphy'
                        url = 'https://github.com/skeet70'
                    }
                }
                scm {
                    url = 'https://github.com/IronCoreLabs/ironoxide-java'
                    connection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                    developerConnection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = VERSION_NAME.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = NEXUS_USERNAME
                password = NEXUS_PASSWORD
            }
        }
    }
}

signing {
    // Only sign artifacts if we can upload them.
    required { NEXUS_USERNAME != "" }
    sign publishing.publications.mavenJava
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
